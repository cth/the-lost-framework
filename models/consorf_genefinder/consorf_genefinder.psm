%========================================================
% Name: 				consorf_genefinder.psm
% Purpose: 			genefinding model for the LoSt framework
%	Author:				Ole Torp Lassen					
% Version:			1
% Last revised:	11.03 2001		
%========================================================
% Main predicate: consorf_main/3
%			Arg1(input) is OrfAnnotations
%			Arg2(input) is conservation annotations
%			Arg3(output)is prediction annotations 
%========================================================
% Basic 2-state paired HMM taking into regard conservation and orf-annotations
% responsible for sequence modelling
%  		driving predictate consorf/3
%========================================================
% states : 
%			begin
%			coding 		(c) annotated 1
%			noncoding (n) annotated 0
%			end
% emission sequences:
%			orf-annotations, 	alphabet={.,<,-,>}
%			conservation, 		alphabet={0,1,2,3,4,5,6,7,8}
%=========================================================
%:-dynamic(genebank_annotation/6).
%:-dynamic(conservation/6).
%:- ddynamic(data/1).
:- set_prism_flag(learn_mode,both).
:- set_prism_flag(log_viterbi,on).
:- set_prism_flag(scaling,log_exp).

data('trainingTest').
values(begin,[c,n]).
values(trans(_),[c,n,end]).
values(emit(_),[
									('.',0),		('.',1),		('.',2),		('.',3),		('.',4),		('.',5),		('.',6),		('.',7),		('.',8),
									('<',0),		('<',1),		('<',2),		('<',3),		('<',4),		('<',5),		('<',6),		('<',7),		('<',8),
									('-',0),		('-',1),		('-',2),		('-',3),		('-',4),		('-',5),		('-',6),		('-',7),		('-',8),
									('>',0),		('>',1),		('>',2),		('>',3),		('>',4),		('>',5),		('>',6),		('>',7),		('>',8)
								]).

consorf(O,C,--A):-
	msw(begin,First),
	consorf_recursive(First,O,C,--A).

consorf_recursive(c,[O|O2],[C|C2],--[1|A2]):-
	msw(emit(c),(O,C)),
	msw(trans(c),Next),
	consorf_recursive(Next, O2, C2, --A2).

consorf_recursive(n,[O|O2],[C|C2],--[0|A2]):-
	msw(emit(n),(O,C)),
	msw(trans(n),Next),
	consorf_recursive(Next, O2, C2, --A2).

consorf_recursive(end,[],[],--[]).


/* predicate for building training data from separate existing files
Make_training_file(Orf_Annot++,Cons_Annot++,Reference_File++,Training_file++)
where 
-	Orf_Annot is a file of facts :  orf_annotation(Id,Left,Right,Dir,Frame,[seq_annotation(Orf_Annot)]).
- Cons_Annot is a file of facts :   conservation(Id,Left,Right,Dir,Frame,[cons_annotation(Cons_Annot)|_Rest_Infos2]).
- Reference_File is a file of facts :         gb(Id,Left,Right,Dir,Frame,Rest_Infos). Ref_Annot is constructed from fact.
- Training_File is a file of goals : 		 consorf(Orf_Annot,Cons_Annot,Ref_Annot). 
such that there is one goal for each Id,Left,Right,Dir,Frame in Orf_Annot.
*/
make_training_file(Orf_Annot,Cons_Annot, Ref_Annot, Training_File):-
	open(Orf_Annot,read,OrfChunk_Stream,[alias(orfin)]),
	open(Training_File,write,Train_Stream,[alias(trainout)]),
	cl(Cons_Annot),
	cl(Ref_Annot),
	
	read(OrfChunk_Stream,OrfChunk_Term),
	make_training_file_rec(OrfChunk_Term, OrfChunk_Stream, Train_Stream),!,	% green cut, for tail-recursion optimization  
	
	%abolish(genebank_annotation/6),
	%abolish(conservation/6),
	close(Train_Stream),
	close(OrfChunk_Stream)
	.

make_training_file_rec(end_of_file,_OrfChunk_Stream,_Train_Stream).	

make_training_file_rec(orf_annotation(Id,Left,Right,Dir,Frame,[seq_annotation(Orf_Annot)]), OrfChunk_Stream, Train_Stream):-
	conservation(Id,Left,Right,Dir,Frame,Extra_Cons_Info),				% ...or chunk mismatch?
	member(cons_annotation(Cons),Extra_Cons_Info),					% ...or missing annotation?
	(
	Cons = [] -> 																						% in case no conservation was reported from blast, replace [] by a list of L 0's
		L is Right - Left + 1, 
		makelist(L,0,Cons_Annot)	
	;
		Cons_Annot = Cons
	),	
	genebank_annotation(Id,Left,Right,Dir,Frame,Extra_Ref_Info),	% ...or chunk mismatch?
	member(seq_annotation(Ref_Annot),Extra_Ref_Info),							% ...or missing annotation?
	
	Training_Goal =.. [consorf,Orf_Annot,Cons_Annot,Ref_Annot],
	
	write(Train_Stream,Training_Goal),
	write(Train_Stream,'.'),
	nl(Train_Stream),
	
	read(OrfChunk_Stream,Next_OrfChunk_Term),
	make_training_file_rec(Next_OrfChunk_Term,OrfChunk_Stream,Train_Stream).

makelist(0,_,[]).
makelist(N,X,[X|Rest]):-
	N > 0,
	M is N-1,
	makelist(M,X,Rest).


/* Examples 							

	%% making training file
	make_training_file(Orf_Annot_File, Cons_Annot_File, Genebank_file, Training_File),
	assert(data(Training_File)),
	learn.
	
	[autoAnnotations].
	%% Learning
	prismAnnot(consorf_genefinder.psm, direct).
	goals(GS), learn(Gs).

	%% Prediction	
	prismAnnot(consorf_genefinder.psm)	
	testO1(O),testC1(C), viterbiAnnot(consorf(O,C,R),P)
	testO2(O),testC2(C), viterbiAnnot(consorf(O,C,R),P)
*/